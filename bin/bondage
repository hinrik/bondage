#!/usr/bin/env perl

use strict;
use warnings;
use App::Bondage;
use App::Bondage::Common;
use Digest::MD5 qw(md5_hex);
use Getopt::Long qw(:config auto_help);
use Pod::Usage;
use POE;
use POE::Component::Daemon;

my $work_dir = "$ENV{HOME}/.bondage";
my $debug = 0;

GetOptions(
    'c|crypt'     => \&encrypt_pass,
    'd|debug'     => \$debug,
    'v|version'   => sub { print "Bondage $VERSION, $HOMEPAGE\n"; exit },
    'w|workdir=s' => \$work_dir,
) or pod2usage();

my $bouncer = App::Bondage->new( Debug => $debug, Work_dir => $work_dir );
POE::Component::Daemon->spawn( detach => 1 ) unless $debug;
$poe_kernel->run();

sub encrypt_pass {
    print "Password:";
    system('stty -echo');
    chomp (my $password = <STDIN>);
    system('stty echo');
    print "\nEncrypted password: " . md5_hex($password, $CRYPT_SALT) . "\n";
    exit;
}

=head1 NAME

bondage - A featureful easy-to-use IRC bouncer

=head1 SYNOPSIS

B<bondage> [options]

 Options:
   -c, --crypt             Prompts you for a password and encrypts it
   -d, --debug             Skip daemonizing and print everything to STDOUT
   -h, --help              Display this help message
   -v, --version           Display version information
   -w DIR, --workdir=DIR   Working directory (default: ~/.bondage)

=head1 DESCRIPTION

This is the command-line program to use Bondage. See the
L<App::Bondage|App::Bondage> documentation for more details.

=head1 AUTHOR

Hinrik E<Ouml>rn SigurE<eth>sson, hinrik.sig@gmail.com

=head1 LICENSE AND COPYRIGHT

Copyright 2008 Hinrik E<Ouml>rn SigurE<eth>sson

This program is free software, you can redistribute it and/or modify
it under the same terms as Perl itself.

